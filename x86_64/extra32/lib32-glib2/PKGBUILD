pkgname=lib32-glib2
pkgver=2.82.0
pkgrel=1
pkgdesc="Low level core library (32-bit)"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL)
arch=(x86_64)
depends=(lib32-pcre lib32-pcre2 lib32-libffi lib32-util-linux
	lib32-zlib glib2 lib32-dbus)
makedepends=(gettext gtk-doc shared-mime-info python3-pip git util-linux
             meson)
gvdbcommit=2b42fc75f09dbe1cd1057580b5782b08f2dcb400
source=(git+https://gitlab.gnome.org/GNOME/glib.git#tag=$pkgver
	git+https://gitlab.gnome.org/GNOME/gvdb.git#commit=$gvdbcommit
	0001-glib-compile-schemas-Remove-noisy-deprecation-warnin.patch
	gio-querymodules-32.hook)

prepare() {
    export PATH=~/.local/bin:$PATH
    pip3 install setuptools
    pip3 install pygments
    pip3 install rst2html5
    pip3 install gi-docgen

    cd glib

    git apply -3 ../0001-glib-compile-schemas-Remove-noisy-deprecation-warnin.patch

    git submodule init
    git submodule set-url subprojects/gvdb "$srcdir/gvdb"
    git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
    export CC="gcc -m32 -mstackrealign"
    export CXX="g++ -m32 -mstackrealign"
    export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

    CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
    meson glib build \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    -D glib_debug=disabled \
    -D selinux=disabled \
    -D introspection=disabled \
    -D documentation=false \
    -D sysprof=disabled \
    -D tests=false \
    -D man=false \
    -D man-pages=false \
    -D gtk_doc=false

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"

    rm -r "$pkgdir"/usr/{share,include,libexec}
    find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -printf 'Removing %P\n' -delete
    mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

    install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
    touch "$pkgdir/usr/lib32/gio/modules/.keep"
}
