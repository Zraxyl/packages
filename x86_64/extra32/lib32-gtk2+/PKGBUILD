_pkgbasename=gtk2
pkgname=lib32-${_pkgbasename}+
pkgver=2.24.33
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (legacy) (32-bit)"
arch=('x86_64')
url="https://www.gtk.org/"
install=gtk2+.install
depends=(lib32-{'pango','cairo','gdk-pixbuf'}
         lib32-lib{xcursor,'xrandr','xi',xinerama,xcomposite,xdamage}
         gtk2+ lib32-cups lib32-at-spi2-core)
makedepends=('python3')
source=(https://download.gnome.org/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz
        0001.patch
	0002.patch)

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"

  patch -p1 -i ../0001.patch
  patch -p1 -i ../0002.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/gtk+-${pkgver}"

  CFLAGS=" -Wno-error=implicit-int -Wno-error=incompatible-pointer-types" \
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libdir=/usr/lib32 \
      --with-xinput=yes

  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}"/usr/bin
  mv gtk-query-immodules-2.0 gtk-query-immodules-2.0-32
  rm -f gtk-builder-convert gtk-demo gtk-update-icon-cache
}
