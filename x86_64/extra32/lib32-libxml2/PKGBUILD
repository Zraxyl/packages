_pkgbasename=libxml2
pkgname=lib32-$_pkgbasename
pkgver=2.13.4
pkgrel=1
pkgdesc='XML parsing library, version 2 (32-bit)'
url='http://www.xmlsoft.org/'
arch=(x86_64)
license=(MIT)
depends=(lib32-zlib lib32-readline lib32-ncurses lib32-xz lib32-icu libxml2)
makedepends=(base-devel)
source=("https://gitlab.gnome.org/GNOME/libxml2/-/archive/v$pkgver/libxml2-v$pkgver.tar.gz")

prepare() {
  cd ${_pkgbasename}-v$pkgver

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  cd ${_pkgbasename}-v$pkgver

  ./configure \
    --prefix=/usr \
    --with-history \
    --without-python \
    --with-legacy \
    --libdir=/usr/lib32

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

package() {
  cd ${_pkgbasename}-v$pkgver

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/usr/lib32/xml2Conf.sh"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
