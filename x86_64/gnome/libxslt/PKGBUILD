pkgname=libxslt
pkgver=1.1.42
pkgrel=1
arch=(x86_64)
depends=(libxml2 libgcrypt)
makedepends=(git docbook-xsl)
checkdepends=(docbook-xml python3)
commitid=165219e5a3fc79cc4f529785e6e60f19a4b9222a  # tags/v1.1.42
source=("git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=$commitid"
        0001-runtest-Print-output-causing-failure.patch)

prepare() {
  cd $pkgname

  # Make xsltproc output reproducible for packages which use it to generate documentation
  patch -Np1 -i ../0001-runtest-Print-output-causing-failure.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd $pkgname

    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-python=/usr/bin/python3

    make
}

package() {
    cd $pkgname

    make DESTDIR="$pkgdir" install
}
