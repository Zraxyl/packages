opkgname=gtk+
pkgname=gtk3+
pkgver=3.24.43
pkgrel=3
pkgdesc="GObject-based multi-platform GUI toolkit"
arch=('x86_64')
install=gtk3.install
depends=(cairo libxcursor libxinerama libxrandr libxi libepoxy gdk-pixbuf
	dconf libxcomposite libxdamage pango shared-mime-info at-spi2-core
	wayland libxkbcommon adwaita-icon-theme json-glib librsvg
	desktop-file-utils mesa cantarell-fonts colord rest cups
	fribidi iso-codes ibus tracker)
makedepends=(gobject-introspection gtk-doc git glib2 sassc meson
	wayland-protocols python3-pygments docbook-xml docbook-xsl xmlto)
source=(https://download.gnome.org/sources/$opkgname/3.24/$opkgname-$pkgver.tar.xz
	gtk-query-immodules-3.0.hook
	la_support.tar.gz.patch)

prepare() {
	mkdir -p build la_support
	cp -f la_support.tar.gz.patch la_support.tar.gz

	# These .la files seem still be used by the xfce source code
	# ( so lets add these back to the new gtk3 src which dosent add these anymore with meson build system )
	tar xf $srcdir/la_support.tar.gz -C $srcdir/la_support

	cd build

	meson \
	--prefix=/usr \
	--sysconfdir=/etc \
	-D x11_backend=true \
	-D wayland_backend=true \
	-D quartz_backend=true \
	-D broadway_backend=true \
	-D tracker3=true \
	-D colord=yes \
	-D man=true \
	-D gtk_doc=true \
	../$opkgname-$pkgver
}

build() {
	cd build

	CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
	meson compile
}

package() {
	cd build

	meson install --destdir=$pkgdir

	install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-3.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

	install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 $srcdir/gtk-query-immodules-3.0.hook

	rm $pkgdir/usr/bin/gtk-update-icon-cache

	# Install .la files
	cp -rf $srcdir/la_support/* $pkgdir/usr/lib/
}
