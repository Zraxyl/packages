pkgname=dbus
pkgver=1.15.8
pkgrel=1
arch=('x86_64')
depends=(libx11 systemd expat audit-userspace)
makedepends=(autoconf-archive docbook-xsl docbook-xml doxygen python3-pip xmlto)
source=(https://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.xz
	dbus-reload.hook
	no-fatal-warnings.diff)

prepare() {
	cd $pkgname-$pkgver

	autoreconf -fvi
}

build() {
	cd $pkgname-$pkgver

	patch -Np1 -i ../no-fatal-warnings.diff

	./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--runstatedir=/run \
	--libexecdir=/usr/lib/dbus-1.0 \
	--with-console-auth-dir=/var/run/console \
	--with-dbus-user=dbus \
	--with-system-pid-file=/var/run/dbus/pid \
	--with-system-socket=/var/run/dbus/system_bus_socket \
	--with-systemdsystemunitdir=/usr/lib/systemd/system \
	--enable-inotify \
	--enable-libaudit \
	--enable-xml-docs \
	--enable-doxygen-docs \
	--enable-systemd \
	--enable-user-session \
	--with-x

	make
}

package() {
	cd $pkgname-$pkgver
	
	make DESTDIR=$pkgdir install

	rm -r $pkgdir/{etc,var}

	mkdir -p $pkgdir/lib
	mv -v $pkgdir/usr/lib/libdbus-1.so.* $pkgdir/lib
	ln -sfv ../../lib/$(readlink $pkgdir/usr/lib/libdbus-1.so) $pkgdir/usr/lib/libdbus-1.so

	echo 'u dbus 81 "System Message Bus"' 	|
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/dbus.conf"

	install -Dt $pkgdir/usr/share/libalpm -m644 $srcdir/*.hook

	rm -rf $pkgdir/var/run $pkgdir/run
}
