pkgname=audit-userspace
pkgver=4.0.2
pkgrel=1
arch=('x86_64')
depends=(glibc bash krb5 libcap-ng libcap openldap)
makedepends=(python3 swig)
source=(https://github.com/linux-audit/$pkgname/archive/refs/tags/v$pkgver.tar.gz
	audit.tmpfiles
	# Patches
	4.0.2-flagtab_h.patch::https://github.com/linux-audit/audit-userspace/commit/6b9b74acba4db9edbab8595474d2323eb7eef51f.patch)

prepare() {
	mkdir -p build

	cd $pkgname-$pkgver

	# fix weird issue with autotools and an unneeded header file: https://github.com/linux-audit/audit-userspace/issues/390
	patch -Np1  -i ../4.0.2-flagtab_h.patch


	# Preconfigure the source
	autoreconf -fvi

	cd ../build

	# Configure the source code
	./../$pkgname-$pkgver/configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--runstatedir=/run \
	--libexecdir=/usr/lib/audit \
	--enable-gssapi-krb5=yes \
	--enable-systemd=yes \
	--enable-zos-remote \
	--with-io_uring=yes \
    --with-libcap-ng=yes \
    --with-python3=yes
}

build() {
	cd build

	make
}

package() {
	cd build

	# Add log dir
	install -vdm 700 "$pkgdir/var/log/$pkgname/"

	# Add rules.d dir
	install -vdm 755 "$pkgdir/etc/audit/rules.d/"

	make DESTDIR=$pkgdir install

	# add tmpfiles.d integration for factory files and file permissions
	install -vDm 644 ../audit.tmpfiles \
	"$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

}
