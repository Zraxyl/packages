pkgname=shadow
pkgver=4.16.0
pkgrel=4
pkgdesc="Password and account management tool suite with support for shadow files and PAM"
arch=('x86_64')
url='https://github.com/shadow-maint/shadow'
license=('BSD')
depends=('linux-pam' 'acl' 'libgcrypt' 'audit-userspace'
  'libxcrypt' 'libcap')
backup=(etc/default/useradd
        etc/login.defs
        etc/pam.d/chpasswd
        etc/pam.d/groupmems
        etc/pam.d/newusers
        etc/pam.d/passwd)
options=(!emptydirs)
source=("https://github.com/shadow-maint/shadow/releases/download/$pkgver/shadow-$pkgver.tar.xz"
        0001-Disable-replaced-tools-their-man-pages-and-PAM-integ.patch
        0002-Adapt-login.defs-for-PAM-and-util-linux.patch
        0003-Add-Arch-Linux-defaults-for-login.defs.patch
        shadow.{timer,service}
        shadow.{sysusers,tmpfiles}
        useradd.defaults)
install=shadow.install

prepare() {
  cd "$pkgname-$pkgver"

  autoreconf -fsiv

  export CFLAGS="$CFLAGS -DEXTRA_CHECK_HOME_DIR"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --disable-account-tools-setuid \
    --with-libpam \
    --with-audit \
    --with-fcaps \
    --without-libbsd \
    --without-su \
    --without-login \
    --without-nologin \
    --without-chfn \
    --without-chsh \
    --without-newgrp \
    --without-vigr \
    --without-vipw \
    --with-group-name-max-length=32 \
    --without-selinux
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"

  # useradd defaults
  install -Dm600 "$srcdir/useradd.defaults" "$pkgdir/etc/default/useradd"

  # systemd units
  install -D -m644 "$srcdir/shadow.timer" "$pkgdir/usr/lib/systemd/system/shadow.timer"
  install -D -m644 "$srcdir/shadow.service" "$pkgdir/usr/lib/systemd/system/shadow.service"
  install -d -m755 "$pkgdir/usr/lib/systemd/system/timers.target.wants"
  ln -s ../shadow.timer "$pkgdir/usr/lib/systemd/system/timers.target.wants/shadow.timer"

  install -vDm 644 ../$pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm 644 ../$pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # adapt executables to match the modes used by tmpfiles.d, so that pacman does not complain:
  chmod 750 "$pkgdir/usr/sbin/groupmems"
}
