pkgname=krb5
pkgver=1.21.3
relver=1.21
pkgrel=1
arch=('x86_64')
depends=(e2fsprogs openldap keyutils libverto lmdb openssl)
makedepends=(perl)
source=(https://web.mit.edu/kerberos/dist/krb5/${relver}/${pkgname}-${pkgver}.tar.gz
	krb5-kadmind.service
	krb5-kdc.service
	krb5-kpropd.service
	krb5-kpropd@.service
	krb5-kpropd.socket)
backup=(
	etc/krb5.conf
	var/lib/krb5kdc/kdc.conf)

prepare() {
	cd $pkgname-$pkgver
	sed -i "/KRB5ROOT=/s/\/local//" src/util/ac_check_krb5.m4
}

build() {
	cd $pkgname-$pkgver/src

	export CFLAGS+=" -fPIC -fno-strict-aliasing -fstack-protector-all"
	export CPPFLAGS+=" -I/usr/include/et"

	./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var/lib \
	--enable-dns-for-realm \
	--with-system-et \
	--with-system-ss \
	--with-ldap \
	--with-lmdb \
	--with-system-verto

	make
}

package() {
	cd $pkgname-$pkgver/src

	make EXAMPLEDIR=/usr/share/doc/$pkgname/examples DESTDIR=$pkgdir install

	install -m 644 plugins/kdb/ldap/libkdb_ldap/kerberos.{ldif,schema} \
	"${pkgdir}/usr/share/doc/${pkgname}/examples"

	install -Dpm 644 config-files/krb5.conf -t "${pkgdir}/etc"
	install -Dpm 644 config-files/kdc.conf -t "${pkgdir}/var/lib/krb5kdc"

	install -Dm 644 "${srcdir}"/krb5-{kadmind.service,kdc.service,kpropd.service,kpropd@.service,kpropd.socket} \
	-t "${pkgdir}/usr/lib/systemd/system"

	rm -rf $pkgdir/var/lib/run
}
