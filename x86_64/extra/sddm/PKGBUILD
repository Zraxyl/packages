pkgname=sddm
pkgver=0.21.0
pkgrel=1
arch=(x86_64)
depends=(xauth qt5 qt6 xorg-server base-fonts double-conversion drivers-xorg
	wayland wayland-protocols mesa glu libglvnd gstreamer-vaapi gst-plugins-good)
makedepends=(extra-cmake-modules python3)
backup=('usr/share/sddm/scripts/wayland-session'
		'usr/share/sddm/scripts/Xsetup'
		'usr/share/sddm/scripts/Xstop'
		'etc/pam.d/sddm'
		'etc/pam.d/sddm-autologin'
		'etc/pam.d/sddm-greeter')
source=(https://github.com/sddm/sddm/archive/refs/tags/v$pkgver.tar.gz)
options=(!strip)

prepare() {
	cmake -B build6 -S $pkgname-$pkgver \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D BUILD_WITH_QT6=ON \
	-D CMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
	-D DBUS_CONFIG_DIR=/usr/share/dbus-1/system.d \
	-D DBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
	-D BUILD_MAN_PAGES=OFF \
	-D UID_MAX=60513

	cmake -B build5 -S $pkgname-$pkgver \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D BUILD_WITH_QT6=OFF \
	-D CMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
	-D DBUS_CONFIG_DIR=/usr/share/dbus-1/system.d \
	-D DBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
	-D BUILD_MAN_PAGES=OFF \
	-D UID_MAX=60513
}

build() {
	cmake --build build6
	cmake --build build5
}

package() {
	DESTDIR="$pkgdir" cmake --install build6

	# QT5 Compat
	DESTDIR="$pkgdir" cmake --install build5/src/greeter
	DESTDIR="$pkgdir" cmake --install build5/components

	# Config
	install -d "$pkgdir"/usr/lib/sddm/sddm.conf.d
	"$pkgdir"/usr/bin/sddm --example-config > "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf

	# Config edits
	sed -r 's|DefaultPath=.*|DefaultPath=/usr/local/sbin:/usr/local/bin:/usr/bin|g' -i "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf

	# Double chmod scripts to be executable!!!
	# Forgetting that will make sessions unusable
	chmod +x "$pkgdir"/usr/share/sddm/scripts/*
}
