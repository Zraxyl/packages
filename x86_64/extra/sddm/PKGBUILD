pkgname=sddm
pkgver=0.19.0
pkgrel=8
arch=(x86_64)
depends=(xauth qt5 xorg-server drunk-fonts double-conversion drunk-drivers-xorg)
makedepends=(extra-cmake-modules python3 qt5)
backup=('usr/share/sddm/scripts/wayland-session'
	'usr/share/sddm/scripts/Xsetup'
        'usr/share/sddm/scripts/Xstop'
        'etc/pam.d/sddm'
        'etc/pam.d/sddm-autologin'
        'etc/pam.d/sddm-greeter')
source=($pkgname-$pkgver.tar.gz::"https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz"
	sddm.sysusers
	sddm.tmpfiles
	tty.conf

	pam-faillock.patch
	sddm-fix-race-pre.patch
	sddm-fix-race.patch
	sddm-desktop-session.patch
	sddm-fix-build.patch)
options=(!strip)

prepare() {
	patch -d $pkgname-$pkgver -p1 < pam-faillock.patch # Port away from deprecated pam_tally2
	patch -d $pkgname-$pkgver -p1 < sddm-fix-race-pre.patch # Make next patch apply
	patch -d $pkgname-$pkgver -p1 < sddm-fix-race.patch # Fix race at startup
	patch -d $pkgname-$pkgver -p1 < sddm-desktop-session.patch # Fix desktop session name
	patch -d $pkgname-$pkgver -p1 < sddm-fix-build.patch # Fix build with Qt post 5.15.2
}


build() {
	cmake -B build -S $pkgname-$pkgver \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D CMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
	-D DBUS_CONFIG_DIR=/usr/share/dbus-1/system.d \
	-D DBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
	-D BUILD_MAN_PAGES=OFF \
	-D UID_MAX=60513

	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build

	install -Dm644 sddm.sysusers "$pkgdir"/usr/lib/sysusers.d/sddm.conf
	install -Dm644 sddm.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/sddm.conf

	# Include modified tty.conf
	install -Dm644 tty.conf "$pkgdir"/etc/sddm.conf.d/tty.conf

	# Double chmod scripts to be executable!!!
	# Forgetting that will make sessions unusable
	chmod +x "$pkgdir"/usr/share/sddm/scripts/*

	install -d "$pkgdir"/usr/lib/sddm/sddm.conf.d
	"$pkgdir"/usr/bin/sddm --example-config > "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf

	# Don't set PATH in sddm.conf
	sed -r 's|DefaultPath=.*|DefaultPath=/usr/sbin:/usr/bin:/bin:/sbin|g' -i "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf

	# Unset InputMethod https://github.com/sddm/sddm/issues/952
	sed -e "/^InputMethod/s/qtvirtualkeyboard//" -i "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf
}
