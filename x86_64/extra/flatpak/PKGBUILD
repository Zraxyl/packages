pkgname=flatpak
pkgver=1.15.12
pkgrel=1
url="https://flatpak.org"
pkgdesc="Linux application sandboxing and distribution framework (formerly xdg-app)"
arch=(x86_64)
depends=(dbus glib2 curl polkit libxau libostree json-glib libarchive socat
	python3 appstream systemd fuse3 bubblewrap xdg-dbus-proxy xdg-desktop-portal
	libglnx gnupg gpgme)
makedepends=(intltool gobject-introspection git xmlto bison python3-pip
	python3-pygments)
options=(emptydirs)
source=(https://github.com/flatpak/flatpak/releases/download/$pkgver/flatpak-$pkgver.tar.xz
	# For desktop entries
	flatpak.sh
	# flatpak repo
	https://dl.flathub.org/repo/flathub.flatpakrepo)

prepare() {
	cd $pkgname-$pkgver

	# Install some py3 modules so flatpak can be compiled
	pip3 install pyparsing
}

build() {
	cd $pkgname-$pkgver

	meson build \
	--prefix=/usr \
	--sysconfdir=/etc \
	--wrap-mode=nofallback \
	-D dbus_config_dir=/usr/share/dbus-1/system.d \
	-D selinux_module=disabled \
	-D system_bubblewrap=bwrap \
	-D system_dbus_proxy=xdg-dbus-proxy \
	-D tests=false \
	-D gir=disabled

	ninja -C build
}

package() {
	cd $pkgname-$pkgver

	DESTDIR=$pkgdir ninja install -C build

	# Fixup mode to match polkit
	install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

	# Install profile.d scriptlet ( overwrite )
	rm -f $pkgdir/etc/profile.d/flatpak.sh
	install -Dt "$pkgdir/etc/profile.d" -m644 $srcdir/flatpak.sh

	# Install official flatpak repo
	install -Dt "$pkgdir/etc/flatpak/remotes.d" $srcdir/flathub.flatpakrepo

	# Misc changes
	mkdir -p $pkgdir/var/lib/ $pkgdir/usr/var/lib/flatpak
	ln -sf /usr/var/lib/flatpak $pkgdir/var/lib/flatpak
}
