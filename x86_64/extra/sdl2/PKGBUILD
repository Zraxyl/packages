pkgname=sdl2
pkgver=2.30.8
pkgrel=2
arch=('x86_64')
depends=('glibc' 'libxext' 'libxrender' 'libx11' 'libglvnd' 'libxcursor'
    'ibus' 'libusb' 'wayland' 'libxkbcommon' 'libdecor' 'libsamplerate')
makedepends=('alsa-lib' 'mesa' 'pulseaudio' 'libxrandr' 'libxinerama'
    'wayland-protocols' 'libxss' 'cmake' 'jack2' 'ninja' 'vulkan-headers'
    'vulkan-icd-loader')
source=("https://www.libsdl.org/release/SDL2-${pkgver}.tar.gz")

prepare() {
    rm -rf build

    cmake \
    -S SDL2-${pkgver} \
    -B build \
    -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D SDL_STATIC=ON \
    -D SDL_LIBSAMPLERATE_SHARED=OFF \
    -D SDL_RPATH=OFF
}

build() {
    cmake --build build
}

package() {
    DESTDIR="${pkgdir}" cmake --install build

    # For some reason, this isn't named correctly and we have to fix it to reflect the actual staticlib name.
    sed -i "s/libSDL2\.a/libSDL2main.a/g" "$pkgdir"/usr/lib/cmake/SDL2/SDL2Targets-noconfig.cmake
}
