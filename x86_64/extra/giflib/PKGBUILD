pkgname=giflib
pkgver=5.2.2
pkgrel=1
pkgdesc='Library for reading and writing gif images'
url='http://giflib.sourceforge.net/'
arch=('x86_64')
depends=('glibc')
makedepends=('base-devel' 'xmlto' 'docbook-xml' 'docbook-xsl')
source=(https://downloads.sourceforge.net/project/giflib/${pkgname}-${pkgver}.tar.gz
        giflib-5.1.9-fix-missing-quantize-API-symbols.patch
        giflib-5.1.9-make-flags.patch)

prepare() {
	cd ${pkgname}-${pkgver}
	patch -Np1 < ../giflib-5.1.9-fix-missing-quantize-API-symbols.patch
	patch -Np1 < ../giflib-5.1.9-make-flags.patch
}

build() {
	cd ${pkgname}-${pkgver}
	make libgif.a libutil.a libgif.so libutil.so
}

package() {
	cd ${pkgname}-${pkgver}
	make PREFIX=/usr DESTDIR="${pkgdir}" install-bin install-lib install-include
	install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
