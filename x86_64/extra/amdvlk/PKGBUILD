pkgname=amdvlk
pkgver=2024.Q3.2
pkgrel=1
arch=(x86_64)
depends=('glibc' 'ncurses')
makedepends=('perl-modules' 'python3-pip' 'libxrandr'
  'cmake' 'ninja' 'git' 'clang' 'python2' 'lld' 'repo' 'directx-shader-compiler')
options=('!lto')

prepare() {
  mkdir -p amdvlk && cd amdvlk

  git config --global user.email "builder@evolinx.org"
  git config --global user.name "Builder Bob"  

  repo init -u https://github.com/GPUOpen-Drivers/AMDVLK.git -b refs/tags/v-$pkgver

  repo sync --force-sync --no-clone-bundle --no-tags
}

build() {
  pip3 install jinja2 pyyaml ruamel.yaml
  cd $srcdir/amdvlk/drivers

  # use lld and clang to fix linking error
  # https://github.com/GPUOpen-Drivers/llpc/issues/1645
  cmake -H. -B builds/Release64 -S xgl \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_C_COMPILER=clang \
    -D CMAKE_CXX_COMPILER=clang++ \
    -D XGL_USE_CLANG=ON \
    -D LLVM_USE_LINKER=lld \
    -D CMAKE_EXE_LINKER_FLAGS='-fuse-ld=lld' \
    -D CMAKE_SHARED_LINKER_FLAGS='-fuse-ld=lld' \
    -D CMAKE_BUILD_TYPE=Release \
    -D BUILD_WAYLAND_SUPPORT=ON \
    -G Ninja

  ninja -C builds/Release64
}

package() {
  cd $srcdir/amdvlk/drivers

  install -m755 -d "${pkgdir}"/usr/lib
  install -m755 -d "${pkgdir}"/usr/share/vulkan/icd.d
  install -m755 -d "${pkgdir}"/usr/share/vulkan/implicit_layer.d

  install builds/Release64/icd/amdvlk64.so "${pkgdir}"/usr/lib/
  install builds/Release64/icd/amd_icd64.json "${pkgdir}"/usr/share/vulkan/icd.d/
  install builds/Release64/icd/amd_icd64.json "${pkgdir}"/usr/share/vulkan/implicit_layer.d/

  sed -i "s#/lib64#/lib#g" "${pkgdir}"/usr/share/vulkan/icd.d/amd_icd64.json
  sed -i "s#/lib64#/lib#g" "${pkgdir}"/usr/share/vulkan/implicit_layer.d/amd_icd64.json

  # Remove included home dir
  rm -rf $pkgdir/home
}
