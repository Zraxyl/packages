pkgname=libva
pkgver=2.22.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=(x86_64)
url=http://intel.github.io/libva/
license=(MIT)
depends=(
	libdrm libglvnd libx11 libxext libxfixes
	wayland)
makedepends=(git mesa meson)
backup=(etc/libva.conf)
commitid=217da1c28336d6a7e9c0c4cb8f1c303968a675f1
source=(git+https://github.com/intel/libva.git#tag=${commitid})

build() {
	CFLAGS+=" -DENABLE_VA_MESSAGING"  # Option missing
	meson libva build \
	--prefix=/usr \
	--buildtype=release

	ninja -C build
}

package() {
	DESTDIR="${pkgdir}" meson install -C build

	install -Dm 644 /dev/stdin "${pkgdir}"/etc/libva.conf <<END
LIBVA_MESSAGING_LEVEL=1
END
}
