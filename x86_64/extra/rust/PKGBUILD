pkgname=rust
pkgver=1.83.0
pkgrel=2
pkgdesc="Systems programming language focused on safety, speed and concurrency"
url=https://www.rust-lang.org/
arch=(x86_64)
license=(MIT Apache)
options=(!emptydirs !strip)
llvm_ver=18.1.8
depends=(gcc llvm curl gcc)
makedepends=(lld lldb clang rust)
source=(
  "https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.xz"
  "https://github.com/llvm/llvm-project/releases/download/llvmorg-$llvm_ver/compiler-rt-$llvm_ver.src.tar.xz"
  config-zraxyl.toml
)

build() {
  cd rustc-$pkgver-src

  cp -f $srcdir/config-zraxyl.toml config.toml

  export RUST_BACKTRACE=1
  export RUST_COMPILER_RT_ROOT="$srcdir/compiler-rt-$llvm_ver.src"
  [[ -d $RUST_COMPILER_RT_ROOT ]]

  DESTDIR="$srcdir/dest-rust" python ./x.py install -j "$(nproc)"

  cd ../dest-rust

  # delete unnecessary files, e.g. files only used for the uninstall script
  rm usr/lib/rustlib/{components,install.log,rust-installer-version,uninstall.sh}
  rm usr/lib/rustlib/manifest-*

  # rustbuild always installs copies of the shared libraries to /usr/lib,
  # overwrite them with symlinks to the per-architecture versions
  ln -srft usr/lib   usr/lib/rustlib/x86_64-unknown-linux-gnu/lib/*.so

  mkdir -p usr/share/bash-completion
  mv etc/bash_completion.d usr/share/bash-completion/completions
}

package() {
  cp -rf dest-rust/* $pkgdir

  find $pkgdir -name "*.old" -exec rm {} \;
}
