pkgname=mesa
pkgver=24.2.2
pkgrel=1
arch=('x86_64')
depends=(
	# X11
	'libxcb' 'libx11' 'libxext' 'libxfixes' 'libxshmfence' 'libxrender' 'libxvmc' 'libxxf86vm'
	# Other
	'wayland' 'wayland-protocols' 'vulkan-icd-loader' 'libomxil-bellagio' 'libclc' 'libglvnd'
	'valgrind' 'libunwind' 'opencl-icd-loader' 'libpciaccess'
	'vdpau' 'glslang' 'libva' 'llvm'  'lm-sensors' 'spirv-llvm-translator'
	'libepoxy' 'gtk3+' 'libc++' 'libx264' 'libx265' 'fontconfig' 'libdrm'
	'libdecor' 'polly'
	)
makedepends=('meson' 'directx-headers' 'vulkan-headers' 'opencl-headers' 'python3-pip' 'clang' 'cmake' 'byacc'
	'bison' 'rust' 'cbindgen' 'rust-bindgen')
source=(https://mesa.freedesktop.org/archive/$pkgname-$pkgver.tar.xz)

prepare() {
	cd $pkgname-$pkgver

	# TODO add our own python3-mako pkg so pip wont be used
	#echo "[!]: Installing mako with pip"
	pip3 install mako pyyaml ply

	sed '1s/python/&3/' -i bin/symbols-check.py
}

build() {
	mkdir -p build

	cd build

	meson \
	--prefix=/usr \
	--buildtype=release \
	-D b_lto=true \
	-D b_ndebug=true \
	-D platforms=x11,wayland \
	-D gallium-drivers=r300,r600,llvmpipe,radeonsi,nouveau,virgl,svga,i915,iris,crocus,zink,d3d12 \
	-D vulkan-drivers=amd,intel,intel_hasvk,swrast \
	-D vulkan-layers=device-select,intel-nullhw,overlay \
	-D dri3=enabled \
	-D egl=enabled \
	-D gallium-extra-hud=true \
	-D gallium-nine=true \
	-D gallium-omx=bellagio \
	-D gallium-opencl=icd \
	-D gallium-va=enabled \
	-D gallium-vdpau=enabled \
	-D gallium-xa=enabled \
	-D gallium-rusticl=true \
	-D rust_std=2021 \
	-D gbm=enabled \
	-D gles1=enabled \
	-D gles2=enabled \
	-D glvnd=enabled \
	-D glx=dri \
	-D libunwind=enabled \
	-D llvm=enabled \
	-D shared-llvm=enabled \
	-D lmsensors=enabled \
	-D osmesa=true \
	-D tools=all \
	-D shared-glapi=enabled \
	-D microsoft-clc=disabled \
	-D video-codecs=vc1dec,h264dec,h264enc,h265dec,h265enc \
	-D vmware-mks-stats=true \
	-D valgrind=enabled \
	../$pkgname-$pkgver/

	ninja -j6
}

package() {
	cd build

	DESTDIR=$pkgdir ninja install
}
