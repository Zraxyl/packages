pkgname=ldc
pkgver=1.35.0
pkgrel=1
arch=(x86_64)
options=(!lto)
depends=(gcc compiler-rt llvm curl lld spirv-llvm-translator)
makedepends=(cmake ninja git)
source=(https://github.com/ldc-developers/ldc/releases/download/v$pkgver/ldc-$pkgver-src.tar.gz
	https://github.com/ldc-developers/phobos/archive/refs/tags/ldc-v$pkgver.tar.gz
	ldc2.conf)

prepare() {
	# Remove old src
	rm -rf ldc ldc-phobos

	# Rename the folders
	mv $srcdir/ldc-$pkgver-src ldc
	mv $srcdir/phobos-ldc-v$pkgver ldc-phobos

	mkdir -p ldc/build

	# Submodule hacks
	cd ldc

	git submodule init
	git config submodule.phobos.url "$srcdir/ldc-phobos"
	git -c protocol.file.allow=always submodule update

	sed -i "s/ldc::llvm_version_base/\"16\"/" driver/linker-gcc.cpp
}

build() {
	export PATH=$P_ROOT/static/ldc/bin:$PATH

	cd ldc/build

	cmake \
	-G Ninja \
	-D CMAKE_C_COMPILER=/usr/bin/clang \
	-D CMAKE_CXX_COMPILER=/usr/bin/clang++ \
	-D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_SKIP_RPATH=ON \
    -D MULTILIB=ON \
    -D INCLUDE_INSTALL_DIR=/usr/include/dlang/ldc \
    -D BUILD_SHARED_LIBS=BOTH \
    -D BUILD_LTO_LIBS=ON \
    -D LDC_WITH_LLD=OFF \
    -D D_COMPILER_FLAGS="-link-defaultlib-shared=false -linker=gold --flto=thin" \
    -D ADDITIONAL_DEFAULT_LDC_SWITCHES="\"-link-defaultlib-shared\"," \
	..

	ninja
}

package() {
	export PATH=$P_ROOT/static/ldc/bin:$PATH

	cd ldc/build

	DESTDIR=$pkgdir ninja install

	ln -sf /usr/share/bash-completion/completions/ldc2 "$pkgdir/usr/share/bash-completion/completions/ldc"

	ln -sf /usr/bin/ldc2 "$pkgdir/usr/bin/ldc"
    ln -sf /usr/bin/ldmd2 "$pkgdir/usr/bin/ldmd"
}
