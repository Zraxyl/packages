pkgname=openssh
pkgver=9.9p1
pkgrel=1
pkgdesc="SSH protocol implementation for remote login, command execution and file transfer"
arch=('x86_64')
depends=(glibc krb5 libxcrypt libedit openssl linux-pam zlib xauth)
makedepends=(base-devel)
backup=(etc/pam.d/sshd
	etc/ssh/ssh_config
	etc/ssh/sshd_config)
source=(https://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/$pkgname-$pkgver.tar.gz
	sshdgenkeys.service
	sshd.service
	ssh-agent.service
	sshd.pam
	openssh.tmpfiles)

prepare() {
	cd $pkgname-$pkgver

	sed '/^#.*\$.*\$$/d' -i ssh{,d}_config

	# prepend configuration option to include drop-in configuration files for sshd_config
	printf "# Include drop-in configurations\nInclude /etc/ssh/sshd_config.d/*.conf\n" | cat - sshd_config > sshd_config.tmp
	mv -v sshd_config.tmp sshd_config

	# prepend configuration option to include drop-in configuration files for ssh_config
	printf "# Include drop-in configurations\nInclude /etc/ssh/ssh_config.d/*.conf\n" | cat - ssh_config > ssh_config.tmp
	mv -v ssh_config.tmp ssh_config

}

build() {
	cd $pkgname-$pkgver

	./configure \
	--prefix=/usr \
	--sbindir=/usr/bin \
	--libexecdir=/usr/lib/ssh \
	--sysconfdir=/etc/ssh \
	--with-libedit \
	--with-security-key-builtin \
	--with-ssl-engine \
	--with-pam \
	--with-privsep-user=nobody \
	--with-kerberos5=/usr \
	--with-xauth=/usr/bin/xauth \
	--with-pid-dir=/run \
	--with-default-path='/bin:/sbin:/usr/bin:/usr/sbin:/usr/local/bin:/usr/local/sbin' \
	--disable-strip \
	--disable-security-key

	make
}

package() {
	cd $pkgname-$pkgver

	make DESTDIR="$pkgdir" install

	install -vdm 755 "$pkgdir/etc/ssh/ssh_config.d"

	install -Dm644 ../sshdgenkeys.service -t "$pkgdir"/usr/lib/systemd/system/
	install -Dm644 ../sshd.service -t "$pkgdir"/usr/lib/systemd/system/
	install -Dm644 ../ssh-agent.service -t "$pkgdir"/usr/lib/systemd/user/
	install -Dm644 ../sshd.pam "$pkgdir"/etc/pam.d/sshd


	install -Dm644 ../openssh.tmpfiles -t "$pkgdir"/usr/lib/tmpfiles.d/
}
