pkgname=wasi-compiler-rt
pkgver=18.1.8
pkgrel=2
pkgdesc='WASI LLVM compiler runtime'
arch=('x86_64')
url='https://compiler-rt.llvm.org/'
psource=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
depends=('wasi-libc')
makedepends=('cmake' 'ninja' 'llvm' 'clang' 'lld')
source=($psource/compiler-rt-${pkgver}.src.tar.xz
	$psource/cmake-$pkgver.src.tar.xz
	WASI.cmake
	wasi-toolchain.cmake)
options=('staticlibs')
prepare() {
	# Platform files have been copied from here https://github.com/WebAssembly/wasi-sdk
	mv cmake-$pkgver.src cmake
	mkdir -p cmake/Platform
	cp WASI.cmake cmake/Platform
}

build() {
	cmake -B build -G Ninja \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_C_COMPILER_WORKS=ON \
	-DCMAKE_CXX_COMPILER_WORKS=ON \
	-DCMAKE_MODULE_PATH="${srcdir}"/make \
	-DCMAKE_TOOLCHAIN_FILE="${srcdir}"/wasi-toolchain.cmake \
	-DCOMPILER_RT_BAREMETAL_BUILD=On \
	-DCOMPILER_RT_INCLUDE_TESTS=OFF \
	-DCOMPILER_RT_HAS_FPIC_FLAG=OFF \
	-DCOMPILER_RT_DEFAULT_TARGET_ONLY=On \
	-DCOMPILER_RT_OS_DIR=wasi \
	-DWASI_SDK_PREFIX=/usr \
	-DCMAKE_C_FLAGS="-fno-exceptions --sysroot=/usr/share/wasi-sysroot" \
	-DCMAKE_INSTALL_PREFIX=/usr/lib/clang/${pkgver%%.*}/ \
	compiler-rt-${pkgver}.src/lib/builtins

	cmake --build build -v
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	ln -sr "${pkgdir}"/usr/lib/clang/${pkgver%%.*}/lib/{wasi,wasip1}
	ln -sr "${pkgdir}"/usr/lib/clang/${pkgver%%.*}/lib/{wasi,wasip2}
}
