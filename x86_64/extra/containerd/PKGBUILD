pkgname=containerd
pkgver=2.0.1
pkgrel=1
pkgdesc="An open and reliable container runtime"
arch=('x86_64')
depends=(runc)
makedepends=(go git libseccomp containers-common go-md2man btrfs-progs)
source=(git+https://github.com/containerd/containerd.git#tag=v${pkgver})

prepare() {
	sed -i 's,/sbin,/usr/bin,;s,/usr/local,/usr,' $pkgname/containerd.service
}

build() {
	cd $pkgname

	export GOFLAGS="-trimpath -mod=readonly -modcacherw"
	make VERSION=v$pkgver PREFIX=/usr GO_BUILD_FLAGS="-trimpath -mod=readonly -modcacherw" GO_GCFLAGS="" EXTRA_LDFLAGS="-buildid="
	make VERSION=v$pkgver man
}

package() {
	cd $pkgname

	make PREFIX=/usr DESTDIR=$pkgdir install

	install -Dm644 containerd.service "$pkgdir"/usr/lib/systemd/system/containerd.service
	install -Dm644 man/*.8 -t "$pkgdir/usr/share/man/man8"
	install -Dm644 man/*.5 -t "$pkgdir/usr/share/man/man5"
	install -Dm644 contrib/autocomplete/ctr "$pkgdir"/usr/share/bash-completion/completions/ctr
	install -Dm644 contrib/autocomplete/zsh_autocomplete "$pkgdir"/usr/share/zsh/site-functions/_ctr
}
