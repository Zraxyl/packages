pkgname=runc
pkgver=1.2.3
pkgrel=1
arch=('x86_64')
pkgdesc="CLI tool for spawning and running containers according to the OCI specification"
depends=(libseccomp criu)
makedepends=(git go go-md2man)
source=(https://github.com/opencontainers/runc/releases/download/v$pkgver/runc.tar.xz)

prepare() {
	mkdir -p src/github.com/opencontainers
	cp -r runc-$pkgver src/github.com/opencontainers/runc
}

build() {
	cd src/github.com/opencontainers/runc
	export GOPATH="$srcdir"
	export BUILDTAGS='seccomp apparmor'
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-trimpath -mod=readonly -modcacherw"
	make runc
}

package() {
	cd src/github.com/opencontainers/runc

	install -Dm755 runc "$pkgdir/usr/bin/runc"
	install -Dm644 contrib/completions/bash/runc \
		"$pkgdir/usr/share/bash-completion/completions/runc"
}
