pkgname=boost
maver=1
miver=86
uver=0
pkgver=$maver.$miver.$uver
pkgrel=1
arch=('x86_64')
depends=(which icu zstd zlib bzip2 python3)
source=(https://boostorg.jfrog.io/artifactory/main/release/$pkgver/source/boost_${maver}_${miver}_${uver}.tar.bz2
	$pkgname-1_86_0-fix-compute-header_patch_1.patch::https://github.com/boostorg/compute/commit/79452d5279831ee59a650c17b71259a821f1a554.patch
	$pkgname-1_86_0-fix-compute-header_patch_2.patch::https://github.com/boostorg/compute/commit/f8e5e3543723379bb6a9dd9d88415a993653e70a.patch
	$pkgname-numpy-2.0.patch::https://github.com/boostorg/python/commit/0474de0f6cc9.patch)

prepare() {
	cd boost_${maver}_${miver}_${uver}

	# support building against NumPy 2.0
	patch -Np1 -d libs/python <../$pkgname-numpy-2.0.patch

	# https://gitlab.archlinux.org/archlinux/packaging/packages/boost/-/issues/3
	patch -Np2 < ../$pkgname-1_86_0-fix-compute-header_patch_1.patch
	patch -Np2 < ../$pkgname-1_86_0-fix-compute-header_patch_2.patch
}

build() {
	cd boost_${maver}_${miver}_${uver}

	pushd tools/build
	./bootstrap.sh --cxxflags="$CXXFLAGS $LDFLAGS"
	./b2 install --prefix="$srcdir"/fakeinstall
	popd

	./bootstrap.sh \
	--with-toolset=gcc \
	--with-icu \
	--with-python=python3

	install -dm755 "$srcdir"/fakeinstall/share/boostbook
	cp -a tools/boostbook/{xsl,dtd} "$srcdir"/fakeinstall/share/boostbook/

	./b2 stage \
	threading=multi \
	variant=release \
	runtime-link=shared \
	--toolset=gcc \
	cxxstd=23 \
	cflags="$CPPFLAGS $CFLAGS -fPIC -O3 -ffat-lto-objects" \
	cxxflags="$CPPFLAGS $CXXFLAGS -fPIC -O3 -ffat-lto-objects" \
	linkflags="$LDFLAGS" \
	--debug-symbols=off \
	link=shared,static \
	--layout=system \
	--prefix="/usr"
}

package() {
	cd boost_${maver}_${miver}_${uver}

	./b2 install \
	--prefix=$pkgdir/usr


	#mkdir -p $pkgdir/usr
	#cp -rf $srcdir/fkinstall/* $pkgdir/usr/
}
