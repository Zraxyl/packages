pkgname=containers-common
pkgver=0.60.4
pkgrel=1
image_pkgver=5.32.2
podman_pkgver=5.2.3
skopeo_pkgver=1.16.1
storage_pkgver=1.55.0
shortnames_pkgver=2023.02.20
arch=('x86_64')
pkgdesc="Configuration files and manpages for containers"
makedepends=(go-md2man)
options=(emptydirs)
backup=(etc/containers/containers.conf
	etc/containers/mounts.conf
	etc/containers/policy.json
	etc/containers/registries.conf
	etc/containers/seccomp.json
	etc/containers/storage.conf)
source=(common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz
	image-${image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${image_pkgver}.tar.gz
	podman-${podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${podman_pkgver}.tar.gz
	skopeo-${skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${skopeo_pkgver}.tar.gz
	storage-${storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${storage_pkgver}.tar.gz
	shortnames-$shortnames_pkgver.tar.gz::https://github.com/containers/shortnames/archive/refs/tags/v$shortnames_pkgver.tar.gz
	mounts.conf
	tmpfiles.conf
	$pkgname-0.48.0-init_path.patch)

prepare() {
	sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i storage-$storage_pkgver/docs/Makefile
	patch -Np1 -d common-$pkgver -i ../$pkgname-0.48.0-init_path.patch
	mkdir -vp build/{man1,man5}
}

build() {
	(
	cd "common-${pkgver}"
	for _man_page in docs/*.md pkg/hooks/docs/*.md; do
	go-md2man -in "$_man_page" -out "${_man_page//.md}"
	done
	)
	(
	cd "image-${image_pkgver}"
	mkdir -vp man5
	mv -v docs/*.5.md man5/
	for _man_page in docs/*.md; do
		go-md2man -in $_man_page -out ../build/man1/$(basename ${_man_page//.md}).1
	done
	for _man_page in man5/*.md; do
		go-md2man -in $_man_page -out ../build/man5/$(basename ${_man_page//.md})
	done
	)
	(
	cd "storage-${storage_pkgver}"
	make -C docs
	)
}

package() {
	# directories
	install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
	install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
	install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
	install -vdm 755 "${pkgdir}/var/lib/containers/"
	install -vDm 644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

	# configs
	install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

	(
	cd "common-${pkgver}"
	# configs
	install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
	install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
	install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
	install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
	# man pages
	install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
	)
	(
	cd "image-${image_pkgver}"
	# configs
	install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
	)
	(
	cd shortnames-$shortnames_pkgver
	install -vDm 644 shortnames.conf "$pkgdir/etc/containers/registries.conf.d/00-shortnames.conf"
	)
	(
	cd "skopeo-${skopeo_pkgver}"
	# configs
	install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
	install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
	)
	(
	cd "storage-${storage_pkgver}"
	# configs
	install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
	install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
	# man pages
	install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
	install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
	)
	install -vDm 644 build/man1/*.1 "$pkgdir/usr/share/man/man1/"
	install -vDm 644 build/man5/*.5 "$pkgdir/usr/share/man/man5/"
}
