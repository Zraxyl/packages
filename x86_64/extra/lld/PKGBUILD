pkgname=lld
pkgver=16.0.5
pkgrel=1
pkgdesc="Linker from the LLVM project"
arch=('x86_64')
url="https://lld.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('llvm')
makedepends=('llvm' 'cmake' 'ninja')
options=('!lto') # LTO causes a couple of test failures
psource=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($psource/$pkgname-$pkgver.src.tar.xz
	$psource/llvm-$pkgver.src.tar.xz
	$psource/cmake-$pkgver.src.tar.xz
	$psource/libunwind-$pkgver.src.tar.xz)

prepare() {
	mv $pkgname-$pkgver.src $pkgname
	mv llvm-$pkgver.src llvm
	mv cmake-$pkgver.src cmake

	# https://bugs.llvm.org/show_bug.cgi?id=49228
	mv libunwind{-$pkgver.src,}

	mkdir -p build
}

build() {
	cd build

	cmake ../$pkgname -G  \
	Ninja \
	-D CMAKE_BUILD_TYPE=Release \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D LLVM_LINK_LLVM_DYLIB=ON \
	-D LLVM_ENABLE_SPHINX=OFF \
	-D LLVM_EXTERNAL_LIT=/usr/bin/lit \
	-D LLVM_INCLUDE_BENCHMARKS=OFF \
	-D LLVM_INCLUDE_TESTS=OFF \
	-D SPHINX_WARNINGS_AS_ERRORS=OFF \
	-D LLVM_BUILD_TESTS=OFF \
	-D LLVM_BUILD_DOCS=OFF \
	-D LLVM_BINUTILS_INCDIR=/usr/include \
	-D LLVM_MAIN_SRC_DIR="$srcdir/llvm"

	ninja
}

package() {
	cd build

	# INSTALL
	DESTDIR="$pkgdir" ninja install
}
