pkgname=qt5-webengine
pkgver=5.15.18
pkgrel=1
pkgdesc="Provides support for web applications using the Chromium browser project"
arch=('x86_64')
depends=(qt5 ffmpeg libevent libvpx libxcomposite libxdamage libxkbfile libxrandr
	libxslt libxss libxtst minizip nss pciutils qt5 snappy pipewire ffmpeg-5)
makedepends=(extra-cmake-modules python3-pip python2-pip python2-setuptools)
commitid_engine=87ceb6a2ef5ee25d56f765dc533728c4ca4787e0 # 5.15.18
source=(qt5-webengine-ffmpeg5.patch
        qt5-webengine-ffmpeg7.patch
        qt5-webengine-pipewire-0.3.patch
        qt5-webengine-icu-75.patch
        qt5-webengine-ninja-1.12.patch
        python3.12-imp.patch
        python3.12-six.patch)
url=https://invent.kde.org/qt/qt

prepare() {
	cd $srcdir

	# Clone and checkout proper qtwebengine src
	if [ ! -d $srcdir/qtwebengine ]; then
		git clone $url/qtwebengine.git -b 5.15.18 qtwebengine
	else
		echo "No clone needed as dir exists"
	fi

	cd qtwebengine

	# neverthless reset the whole source
	git reset --hard $commitid_engine

	# Now lets checkout the modules ( + force reset these from any old patches )
	git submodule update --force --init src/3rdparty
	git submodule update --init --recursive
	git submodule foreach --recursive git reset --hard

	# patching time
	set +e
	# Fix build with ffmpeg 5
	patch -p1 -d src/3rdparty -i "$srcdir"/qt5-webengine-ffmpeg5.patch
	# Port to pipewire 0.3
	patch -p1 -d src/3rdparty -i "$srcdir"/qt5-webengine-pipewire-0.3.patch
	# Fix build with ICU 75
	patch -p2 -d src/3rdparty/chromium -i "$srcdir"/qt5-webengine-icu-75.patch
	# Fix build with ninja 1.12
	patch -p2 -d src/3rdparty/chromium -i "$srcdir"/qt5-webengine-ninja-1.12.patch
	# Fix build with python 3.12 - patch from Debian
	patch -p1 -d src/3rdparty/chromium -i "$srcdir"/python3.12-imp.patch
	# Fix build with python 3.12 - patch from Debian
	patch -p1 -d src/3rdparty/chromium -i "$srcdir"/python3.12-six.patch
	# Fix build with ffmpeg 7 - Chromium patches
	patch -p1 -d src/3rdparty/chromium -i "$srcdir"/qt5-webengine-ffmpeg7.patch
	set -e
}

build() {
	mkdir -p qtwebengine/build
	cd qtwebengine/build

        export LDFLAGS+=" -L/usr/lib/ffmpeg5.1"
        export PKG_CONFIG_PATH="/usr/lib/ffmpeg5.1/pkgconfig/:$PKG_CONFIG_PATH"

	qmake ../ \
	CONFIG+=force_debug_info \
	EXTRA_GN="use_lld=true is_clang=true clang_use_chrome_plugins=false"  -- \
	-proprietary-codecs \
	-webp \
	-spellchecker \
	-webengine-icu \
	-webengine-kerberos \
	-webengine-webrtc-pipewire \
	-webengine-python-version python3

	make
}

package() {
	cd qtwebengine/build

	make INSTALL_ROOT="$pkgdir" install
}
